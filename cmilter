#!/usr/bin/perl -w

#  Copyright (C) 2012-2015 Nigel Horne <njh@bandsman.co.uk>
#
#  This program is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 2 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program; if not, write to the Free Software
#  Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston,
#  MA 02110-1301, USA.
#
# This program is a milter (i.e. a sendmail filter/plug-in) which blocks
# connections from remote sites in countries that we know we won't get e-mails
# from.
#
# Before installing:
#	modify the blacklist hash table
#	apt-get install libgeoip-dev
#	choose your caching algorithm (look for $cache = in this code)
#
# To install, simply put this script into /usr/local/etc/clmilter

# To run add this to /etc/rc.local:
#	rm -f /var/tmp/cmilter
#	/usr/local/etc/cmilter local:/var/tmp/cmilter
#
# INPUT_MAIL_FILTER(`cmilter', `S=local:/var/tmp/cmilter, F=T, T=S:4m;R:4m')dnl

# The latest development version is available
#	from https://github.com/nigelhorne/cmilter

# Version 1.0 9/8/12: First release
# Version 1.1 10/8/12:
#	Added resilience for connection to IANA
#	Cache Whois lookups
# Version 1.2 13/8/12:
#	Increased Whois cache time
#	Prefer the GeoIP database
# Version 1.3
#	Use ftok($0) as the key for the shared memory area

# TODO: Use Proc::Daemon to daemonize ourself

use strict;
use diagnostics;
use warnings;

use Sendmail::PMilter qw(:all);
use Data::Validate::IP;
use Net::DNS;
use Geo::IP;
use Net::Whois::IP;
use Net::Whois::IANA;
use Locale::Country;
use CHI;
use IPC::SysV;

my %blacklist = (
	'MD' => 1,
	'RU' => 1,
	'CN' => 1,
	'BR' => 1,
	'UY' => 1,
	'TR' => 1,
	'MA' => 1,
	'VE' => 1,
	'SA' => 1,
	'CY' => 1,
	'CO' => 1,
	'KR' => 1,
	'MX' => 1,
	'IN' => 1,
	'RS' => 1,
	'PK' => 1,
	'TW' => 1,
);

my $milter = Sendmail::PMilter->new();
my $resolver = Net::DNS::Resolver->new();
my $iana = Net::Whois::IANA->new();
my $gi = Geo::IP->new(GEOIP_STANDARD);

# Choose your caching mechanism depending on your needs
# my $cache = CHI->new(
	# driver => 'File',
	# root_dir => '/tmp',
	# namespace => $0,
# );
# my $cache = CHI->new(driver => 'Memory', datastore => {});
my $cache = CHI->new(driver => 'SharedMem', shmkey => IPC::SysV::ftok($0));

my %cbs;
$cbs{'connect'} = sub {
	my ($ctx, $hostname) = @_;

	# print "connection from $hostname\n";

	my $ip;

	if($hostname =~ /\[(.+)]/) {
		$ip = $1;
		unless(is_ipv4($ip)) {
			warn("$ip isn't a valid IPv4 address");
			$ctx->setreply(554, "5.7.1", "$ip isn't a valid IPv4 address");
			return SMFIS_REJECT;
		}
	} else {
		my $query = $resolver->search($hostname);

		if($query) {
			foreach my $rr ($query->answer()) {
				next unless($rr->type() eq 'A');
				$ip = $rr->address();
				last;
			}
		} else {
			warn("Can't resolve hostname $hostname, " . $resolver->errorstring());
			$ctx->setreply(554, "5.7.1", "Can't resolve hostname $hostname");
			return SMFIS_REJECT;
		}

		unless($ip) {
			warn("hostname $hostname, has no A record - rejecting");
			$ctx->setreply(554, "5.7.1", "hostname $hostname, has no A record - rejecting");
			return SMFIS_REJECT;
		}

		# print "resolved to $ip\n";
	}

	if(is_private_ipv4($ip) || is_loopback_ipv4($ip)) {
		return SMFIS_CONTINUE;
	}

	my $country = $gi->country_code_by_addr($ip);

	unless($country) {
		$country = $cache->get($ip);
	}
	unless(defined($country)) {
		# print "$ip: MISS\n";
		my $whois = Net::Whois::IP::whoisip_query($ip);

		if(defined($whois) && exists($whois->{Country})) {
			$country = $whois->{Country};
		}

		if(!defined($country) || ($country !~ /^[A-Z]{2}$/i)) {
			eval {
				$iana->whois_query(-ip => $ip);
			};
			if($@) {
				warn $@;
				$country = undef;
				# Create new object
				my $iana = Net::Whois::IANA->new();
			} else {
				$country = $iana->country();
				if($country !~ /^[A-Z]{2}$/i) {
					$country = undef;
				}
			}
		}
		$cache->set($ip, $country, '1 week');
	} else {
		# print "$ip: HIT\n";
	}

	if($country) {
		$country = uc($country);
		print "$ip: $country\n";
		if(exists($blacklist{$country})) {
			$country = code2country($country);
			warn("We don't accept emails from $country");
			$ctx->setreply(554, "5.7.1", "We don't accept emails from $country");
			return SMFIS_REJECT;
		}
	} else {
		warn("Can't determine the country for IP $ip");
		# Should tempfail with 220 in connect(), however
		# Sendmail::PMilter forces the argument to be 4xx or 5xx
		# $ctx->setreply(220, '4.7.1', "Can't determine the country for IP $ip");
		$ctx->setreply(451, '4.7.1', "Can't determine the country for IP $ip");
		# In the realworld, there is an argument for REJECT here
		return SMFIS_TEMPFAIL;
	}

	return SMFIS_CONTINUE;
};

$milter->auto_setconn('cmilter');
$milter->register('cmilter', \%cbs, SMFI_CURR_ACTS);
$milter->main();
